/*
 * Copyright 2017 Nicola Atzei
 */

grammar it.unica.tcs.BitcoinTM with org.eclipse.xtext.common.Terminals

generate bitcoinTM "http://www.unica.it/tcs/BitcoinTM"

import "http://www.eclipse.org/emf/2002/Ecore" as ecore
    
Model:
	PackageDeclaration;

PackageDeclaration:
    'package' name=QualifiedName
    	imports+=Import*
		net=Network?
		declarations+=Declaration*
;

QualifiedName: ID ('.' ID)*;

Import:
    'import' importedNamespace=QualifiedNameWithWildcard;
    
QualifiedNameWithWildcard:
    QualifiedName '.*'?;

Declaration: 
  TransactionDeclaration
| ParticipantDeclaration; 

Network:
	{Network} "network" (testnet?="testnet" | "mainnet");

/*
 * keys
 */
KeyDeclaration: 
	{KeyDeclaration} "key" name=ID "=" (value=STRING | placeholder?="_");
	
/*
 * transactions
 */
TransactionDeclaration:
	UserTransactionDeclaration | SerialTransactionDeclaration
;

UserTransactionDeclaration:
	"transaction" name=ID ("(" (params+=Parameter ("," params+=Parameter)*)? ")")? body=TransactionBody;

SerialTransactionDeclaration:
	isSerial?='serial' 'transaction' name=ID '=' (bytes=TXSERIAL | id=TXID);
	
TransactionBody: {TransactionBody} "{"
	("input"  "=" (inputs+=(Input|InputPlaceholder)  | (isMultiIn?="["  inputs+=Input   (";" inputs+=Input)*   "]") ))?
	("output" "=" (outputs+=Output| (isMultiOut?="[" outputs+=Output (";" outputs+=Output)* "]") ))?
	("after" tlock=Tlock)?
"}";


Input:
	txRef=TransactionReference ("@" outpoint=INT)? ":" (exps+=ScriptAtomic)+ ("["redeemScript=Script"]")?;

InputPlaceholder returns Input:
	placeholder?="_";

Output:
	value=BitcoinValue ":" script=Script;

BitcoinValue:
	exp=ScriptExpression;

Tlock: times+=Time ("&&" times+=Time)*;

Time: AbsoluteTime | RelativeTime;

AbsoluteTime:
	block?="block" value=INT | date?="date" value=TIMESTAMP;

RelativeTime:
	value=Delay "from" tx=[TransactionDeclaration];

TransactionReference:
	tx=[TransactionDeclaration] 
	("("(actualParams+=(Literal|VariableReference) ("," actualParams+=(Literal|VariableReference))*)?")")?;

Script:
	(lambda?="fun"  "(" (params+=Parameter ("," params+=Parameter)*)? ")" ".")? exp=ScriptExpression;

Parameter:
	name=ID (":" paramType=ParameterType)?;



Expression:		/* just to force some hierarchy and resolve ambiguity */
	"1" ScriptExpression | "2" ProtocolExpression;


/*
 * script expressions
 */
ScriptExpression:
    OrScriptExpression;

OrScriptExpression returns ScriptExpression:
	AndScriptExpression (({OrScriptExpression.left=current} => '||' ) right=AndScriptExpression)*;

AndScriptExpression returns ScriptExpression:
	ScriptComparison (({AndScriptExpression.left=current} '&&') right=ScriptComparison)*;

ScriptComparison returns ScriptExpression:
       ScriptEquals (({ScriptComparison.left=current} op=('>=' | '<=' | '>' | '<') ) right=ScriptEquals)*;

ScriptEquals returns ScriptExpression:
       ScritpAddition (({ScriptEquals.left=current} op=("==" | "!=") ) right=ScritpAddition)*;
 
ScritpAddition returns ScriptExpression:
    ScriptPrefixed (({ScriptPlus.left=current} '+' | {ScriptMinus.left=current} '-') right=ScriptPrefixed)*; 

ScriptPrefixed returns ScriptExpression:
    {ScriptBooleanNegation} =>"!" exp=ScriptAtomic | /* right associativity */
    {ScriptArithmeticSigned} =>"-" exp=ScriptAtomic | /* right associativity */
    ScriptAtomic;

ScriptAtomic returns ScriptExpression:
    '(' ScriptExpression ')' 
|   Literal 
|   VariableReference
|   Signature 
|	Versig 
|	Hash 
|	IfThenElse 
|	Min 
|   Max
|   Between
|   Size
|	AfterTimeLock;

VariableReference: ref=[Parameter];

Literal:
	NumberLiteral 
|	StringLiteral
|	BooleanLiteral
|	HashLiteral;

NumberLiteral: value=Number;
StringLiteral: value=STRING;
BooleanLiteral: ^true?='true' | {BooleanLiteral} 'false';
HashLiteral: value=BTC_HASH;

IfThenElse:
	'if' if=ScriptExpression
	"then" then=ScriptAtomic
	'else' else=ScriptAtomic;

Signature: "sig" key=[KeyDeclaration|QualifiedName] (modifier=Modifier)?;

enum Modifier: 
	AIAO 
|	AISO 
|	AINO
|	SIAO 
|	SISO
|	SINO;
	
Versig:
	"versig" "(" 
		pubkeys+=[KeyDeclaration|QualifiedName] ("," pubkeys+=[KeyDeclaration|QualifiedName])* ";"
		signatures+=ScriptExpression ("," signatures+=ScriptExpression)* 
	")";

Hash: type=('sha256'|'ripemd160'|'hash256'|'hash160') "(" value=ScriptExpression ")";

Min: "min" "(" left=ScriptExpression "," right=ScriptExpression ")";
Max: "max" "(" left=ScriptExpression"," right=ScriptExpression ")";
Between: "between" "(" value=ScriptExpression "," left=ScriptExpression":" right=ScriptExpression ")";
Size: "size" "(" value=ScriptExpression ")";
AfterTimeLock: "after" timelock=Time ":" continuation=ScriptAtomic;


ParameterType returns Type:
	BasicType;

Type:
	BasicType 
|	TypeVariable;

BasicType:
	{IntType} value=IntTypeEnum 
|	{HashType} value=HashTypeEnum
|   {StringType} value=StringTypeEnum 
|   {BooleanType} value=BoolTypeEnum
|   {SignatureType} value=SignatureTypeEnum
|   {TransactionType} value=TransactionTypeEnum;

TypeVariable:
	value=ID;

enum IntTypeEnum: int;
enum HashTypeEnum: hash;
enum StringTypeEnum: string;
enum BoolTypeEnum: bool | boolean;
enum SignatureTypeEnum: signature;
enum TransactionTypeEnum: transaction;

Typeable:
  TransactionDeclaration
| TransactionBody
| Parameter
| Input
| Output 
| ParticipantDeclaration
| ProcessDeclaration
| ProcessReference
| Process
| Prefix
| ProtocolTransactionReference
| Expression
;

Number returns ecore::EInt:
	INT ('.' INT)? "BTC"?
|	INT_HEX;

Delay returns ecore::EInt:
	MinutesDelay | HoursDelay | DayDelay | BlockDelay;

MinutesDelay returns ecore::EInt: INT "minutes";
HoursDelay   returns ecore::EInt: INT "hours";
DayDelay     returns ecore::EInt: INT "days";
BlockDelay   returns ecore::EInt: INT "blocks";

terminal INT_HEX returns ecore::EInt:
	('0x'|'0X') ('0'..'9'|'a'..'f'|'A'..'F') 
	(('0'..'9'|'a'..'f'|'A'..'F'|'_')* ('0'..'9'|'a'..'f'|'A'..'F') )?;	// java-like hexadecimal number

@Override	
terminal INT returns ecore::EInt:
	'0'..'9' (('0'..'9'|'_')* '0'..'9')?  ; 	// java-like integer

terminal BASE58: 
	'base58:' ('1'|'2'|'3'|'4'|'5'|'6'|'7'|'8'|'9'|'A'|'B'|'C'|'D'|'E'|'F'|'G'|'H'|'J'|'K'|'L'|'M'|'N'|'P'|'Q'|'R'|'S'|'T'|'U'|'V'|'W'|'X'|'Y'|'Z'|'a'|'b'|'c'|'d'|'e'|'f'|'g'|'h'|'i'|'j'|'k'|'m'|'n'|'o'|'p'|'q'|'r'|'s'|'t'|'u'|'v'|'w'|'x'|'y'|'z')+;

terminal BTC_HASH returns ecore::EByteArray: SHA256 | RIPEMD160 | HASH256 | HASH160;
terminal SHA256:	'sha256:'    ('0'..'9'|'a'..'f'|'A'..'F')+;
terminal RIPEMD160: 'ripemd160:' ('0'..'9'|'a'..'f'|'A'..'F')+;
terminal HASH256:   'hash256:'   ('0'..'9'|'a'..'f'|'A'..'F')+;
terminal HASH160:   'hash160:'   ('0'..'9'|'a'..'f'|'A'..'F')+;
terminal TXSERIAL:  'hex:'       ('0'..'9'|'a'..'f'|'A'..'F')+;
terminal TXID:      'txid:'      ('0'..'9'|'a'..'f'|'A'..'F')+;
	
terminal TIMESTAMP returns ecore::EInt: 
	YEAR '-' MONTH '-' DAY	// YYYY-MM-dd
	("T" HOUR ":" MINUTE 					// THH:mm
		(":" SECOND 						// :ss
			(OFFSET_SIGN OFFSET_HOUR ":" OFFSET_MINUTE)?	// +/- HH:mm
		)?
	)?;

terminal YEAR: ('1'..'9')('0'..'9')('0'..'9')('0'..'9');
terminal MONTH: ('0'..'9')('1'..'9');
terminal DAY: ('0'..'9')('1'..'9');	 
terminal HOUR: ('0'..'2')('0'..'9');
terminal MINUTE: ('0'..'5')('0'..'9');
terminal SECOND: ('0'..'5')('0'..'9');
terminal OFFSET_SIGN: "+"|"-";
terminal OFFSET_HOUR: '0'('0'..'9') | '1'('0'..'2');
terminal OFFSET_MINUTE: '0''0' | '3''0';


/*
 * Endpoints contracts
 */
ParticipantDeclaration: 'participant' name=ID '{' 
	(process=Process)? & (keys+=KeyDeclaration | defs+=ProcessDeclaration)*
'}';


ProcessDeclaration: 'process' name=ID ("(" (params+=Parameter ("," params+=Parameter)*)? ")")? '=' process=Process;
ProcessReference: ref=[ProcessDeclaration] ("("(actualParams+=ProtocolExpression ("," actualParams+=ProtocolExpression)*)?")")?;

Process:
  SingleChoice 
| ProtocolIfThenElse
| ProcessReference
| '(' Parallel ')'
;

Parallel returns Process:
	Process (({Parallel.left=current} => '|' ) right=Process)*;

Choice:
	actions+=Prefix ('+' actions+=Prefix)*;

SingleChoice returns Choice:
	actions+=Prefix;

NextProcess returns Process:
  SingleChoice
| ProtocolIfThenElse
| ProcessReference
| '(' Process ')'
;

Prefix:	(Ask|Put|Send|Receive|Assert|Tau) ('->' next=NextProcess)?;

Tau: {Tau} 't';

Ask: "ask" (txRefs+=ProtocolTransactionReference
		|'{'(txRefs+=ProtocolTransactionReference ) (',' txRefs+=ProtocolTransactionReference)* '}') ;
	
Put: 'put' (
	  	txRefs+=ProtocolTransactionReference
		|'{'(txRefs+=ProtocolTransactionReference ) (',' txRefs+=ProtocolTransactionReference)* '}');

Send: 'send' message=ProtocolExpression 'to' p=[ParticipantDeclaration];
Receive: 'receive' var=Parameter 'from' p=[ParticipantDeclaration];
Assert:	'check' exp=ProtocolExpression;

ProtocolIfThenElse: 
'if' exp=ProtocolExpression 
'then' then=NextProcess 
(=> 'else' else=NextProcess)?;


ProtocolTransactionReference:
	txRef=TransactionReference (substitution=TransactionBody)?;

ProtocolExpression:
    OrProtocolExpression;

OrProtocolExpression returns ProtocolExpression:
	AndProtocolExpression (({OrProtocolExpression.left=current} => '||' ) right=AndProtocolExpression)*;

AndProtocolExpression returns ProtocolExpression:
	ProtocolComparison (({AndProtocolExpression.left=current} '&&') right=ProtocolComparison)*;

ProtocolComparison returns ProtocolExpression:
    ProtocolEquals (({ProtocolComparison.left=current} op=('>=' | '<=' | '>' | '<') ) right=ProtocolEquals)*;

ProtocolEquals returns ProtocolExpression:
    ProtocolAddition (({ProtocolEquals.left=current} op=("==" | "!=") ) right=ProtocolAddition)*;
 
ProtocolAddition returns ProtocolExpression:
    ProtocolMultiply (({ProtocolPlus.left=current} '+' | {ProtocolMinus.left=current} '-') right=ProtocolMultiply)*; 

ProtocolMultiply returns ProtocolExpression:
    ProtocolPrefixed (({ProtocolTimes.left=current} '*' | {ProtocolDiv.left=current} '/') right=ProtocolPrefixed)*; 


ProtocolPrefixed returns ProtocolExpression:
    {ProtocolBooleanNegation} =>"!" exp=ProtocolAtomic | /* right associativity */
    {ProtocolArithmeticSigned} =>"-" exp=ProtocolAtomic | /* right associativity */
    ProtocolAtomic;

ProtocolAtomic returns ProtocolExpression:
    '(' ProtocolExpression ')' 
|   Literal 
|   VariableReference
|	Hash 
|	SignOp		// signature operation, returns bytes or string 
|	VersigOp		// versig operation, returns boolean
;

SignOp:	"sig" key=[KeyDeclaration|QualifiedName] (modifier=Modifier)? "of" tx=ProtocolTransactionReference;
VersigOp: "versig" "(" 
		pubkeys+=[KeyDeclaration|QualifiedName] ("," pubkeys+=[KeyDeclaration|QualifiedName])* ";"
		signatures+=ScriptExpression ("," signatures+=ScriptExpression)* ")" 
		"using" tx=ProtocolTransactionReference;
		
		