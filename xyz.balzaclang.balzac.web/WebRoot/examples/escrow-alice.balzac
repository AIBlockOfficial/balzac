/*
 * Escrow: Alice's view
 */

// Alice's public key
const kApub = pubkey:03ff41f23b70b1c83b01914eb223d7a97a6c2b24e9a9ef2762bf25ed1c1b83c9c3

// Alice's private key
const kA = key:cSthBXr8YQAexpKeh22LB9PdextVE1UJeahmyns5LzcmMDSy59L4

// Bob's public key
const kBpub = pubkey:02a575580456e1a979cd0f563fe99b1cf6ea0368f3c8f943070ae26f04c90575fe

//
// 1) Alice's funds
//

// tx with Alice's funds, redeemable with Alice's private key
transaction A_funds {
    input = _
    output = 1 BTC: fun(x). versig(kApub; x)
}

//
// 2) Alice spends the funds
//

transaction T {
    input = A_funds: sig(kA)
    output = 1 BTC: fun(x, y). versig(kApub, kBpub; x, y)
}

//
// 3.1) Alice creates a signature to send to Bob
//

transaction T1(sigA:signature, sigB:signature, pubK:pubkey)  {
    input = T: sigA sigB
    output = 1 BTC: fun(x). versig(pubK; x)
}

// signature of T1 that send the money to Bob
const sigA_to_Bob = sig(kA) of T1(_,_,kBpub)

//
// 3.2) Alice receives Bob's signature and spends T
//

const sigB = sig:30450221009baced12e4af1d2b94f6dadfe9cae8d986ad91017f52cf50c8d94d12f4e5f6b3022069c5dcd12e928d71d3c8393ebf695d4211e24d1a37225984332f0b7af8148aa201
const sigA = sig(kA) of T1(_,_,kApub)

eval T1(sigA, sigB, kApub)